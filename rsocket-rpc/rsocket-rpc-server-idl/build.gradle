plugins {
    id 'com.google.protobuf' version '0.8.7'
    id 'java'
    id 'idea'
}

group 'com.example'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
//    mavenCentral()
    jcenter()
    maven {
        url 'https://dl.bintray.com/netifi/netifi-oss/'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation("io.rsocket.rpc:rsocket-rpc-core:0.2.17")
    implementation("com.google.protobuf:protobuf-java:3.8.0")
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.6.1'
    }
    plugins {
        rsocketRpc {
            artifact = 'io.rsocket.rpc:rsocket-rpc-protobuf:0.2.17'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            rsocketRpc {}
        }
    }
}

// If you use Intellij add this so it can find the generated classes
idea {
    module {
        sourceDirs += file("src/main/proto")
        sourceDirs += file("src/generated/main/java")
        sourceDirs += file("src/generated/main/rsocketRpc")

        generatedSourceDirs += file('src/generated/main/java')
        generatedSourceDirs += file('src/generated/main/rsocketRpc')
    }
}

// clean generated code
clean {
    delete 'src/generated/main'
}